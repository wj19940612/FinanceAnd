apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig { // default is prod
        applicationId "com.sbai.finance"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 40
        versionName "2.0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField('String', 'HOST', '\"lemi.ailemi.com\"')
        buildConfigField('boolean', 'IS_PROD', 'true')

        manifestPlaceholders = [
                GETUI_APP_ID    : "e3evlSF5eY65Jukyyjugo4",
                GETUI_APP_KEY   : "BCSENAMNPp8PUog0E52y98",
                GETUI_APP_SECRET: "XSHhMzgCN16WmzYwdmwMh",
                UMENG_APP_KEY   : "5923f21eaed1793376000b30",
                UMENG_CHANNEL   : "android"
        ]

        // the ndk and manifest configs are for getui
        ndk {
            abiFilters "armeabi", "armeabi-v7a", "arm64-v8a", "mips", "mips64", "x86", "x86_64"
        }
    }

    java.util.Properties properties = new Properties();
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    signingConfigs {
        release {
            storeFile file(properties.get('keystore_storeFile'))
            storePassword properties.get('keystore_storePassword')
            keyAlias properties.get('keystore_keyAlias')
            keyPassword properties.get('keystore_keyPassword')
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            ext.enableCrashlytics = false
        }
    }

    packagingOptions {
        exclude "META-INF/INDEX.LIST" // will not include INDEX file
        exclude "META-INF/io.netty.versions.properties"
        // for getui
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def file = output.outputFile
            def newFileName = file.name.replace(".apk", "-${variant.versionName}.apk")
            output.outputFile = new File(file.parent, newFileName)
        }
    }

    aaptOptions {
        cruncherEnabled true // !!set true when package release apk
    }

    dexOptions {
        preDexLibraries true
        maxProcessCount 8
        javaMaxHeapSize '2g'
    }

    productFlavors {

        dev {
            minSdkVersion 19
            versionNameSuffix '-dev'
            applicationIdSuffix '.dev'
            resConfigs "xxhdpi"
//            buildConfigField('String', 'HOST', '\"192.168.1.7\"')
            buildConfigField('String', 'HOST', '\"devlemi.ailemi.com\"')
            buildConfigField('boolean', 'IS_PROD', 'false')

            manifestPlaceholders = [
                    GETUI_APP_ID    : "3OXiQAgdLK74blL5qrfvn7",
                    GETUI_APP_KEY   : "Cm5IpNQNesA7Pg6hQI6wq8",
                    GETUI_APP_SECRET: "dIHXqXMBzt6rhfAFui4Gt9",
                    UMENG_APP_KEY   : "591a6342677baa75060024bf",
                    UMENG_CHANNEL   : "android"
            ]
        }

        alpha {
            applicationId "com.sbai.finance.alpha"
            versionNameSuffix '-Alpha'
            versionCode 105
            versionName "1.2.0913.2"
            buildConfigField('String', 'HOST', '\"testlemi.ailemi.com\"')
            buildConfigField('boolean', 'IS_PROD', 'false')

            manifestPlaceholders = [
                    GETUI_APP_ID    : "3OXiQAgdLK74blL5qrfvn7",
                    GETUI_APP_KEY   : "Cm5IpNQNesA7Pg6hQI6wq8",
                    GETUI_APP_SECRET: "dIHXqXMBzt6rhfAFui4Gt9",
                    UMENG_APP_KEY   : "591a6342677baa75060024bf",
                    UMENG_CHANNEL   : "android"
            ]
        }

        // channel use default (prod) configuration
        tencent {}
        m360 {}
        xiaomi {}
        baidu {}
        huawei {}
        vivo {}
        oppo {}
        others {}
    }

    productFlavors.all { flavor ->
        if (flavor.name != 'dev' && flavor.name != 'alpha') {
            flavor.manifestPlaceholders = [UMENG_CHANNEL: name]
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile project(':httplib')
    compile project(':chart')
    compile project(':glide')

    compile('cn.qqtheme.framework:WheelPicker:1.4.4') {
        exclude group: 'com.android.support'
    }
    compile files('libs/alipaySdk-20170623-proguard.jar')

    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.getui:sdk:2.10.2.0'
    compile 'com.umeng.analytics:analytics:latest.integration'
    compile 'com.github.chrisbanes:PhotoView:1.3.1'
    compile 'com.koushikdutta.async:androidasync:2.+'

    compile 'com.jakewharton:butterknife:8.0.1'
    apt 'com.jakewharton:butterknife-compiler:8.0.1'

    compile 'pl.droidsonroids.gif:android-gif-drawable:1.2.8'

    testCompile 'junit:junit:4.12'
}


